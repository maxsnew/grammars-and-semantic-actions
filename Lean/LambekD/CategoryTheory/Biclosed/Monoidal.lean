-- TODO : merge this properly into mathlib
-- Refactor of Mathlib.CategoryTheory.Closed.Monoidal
/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Bhavik Mehta, Daniel Carranza, JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Monoidal.Functor
import Mathlib.CategoryTheory.Monoidal.CoherenceLemmas
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Adjunction.Parametrized

/-!
# Closed monoidal categories

Define left/right closed objects and left/right closed monoidal categories.
A biclosed monoidal category is one that is both left and right closed.

## TODO
- Some of the theorems proved about cartesian closed categories
  should be generalised and moved to this file.

- Build the left closed structures in terms of the right ones by composing
  with flip of a Bifunctor
-/


universe v u uâ‚‚ vâ‚‚

namespace CategoryTheory

open Category MonoidalCategory

-- Note that this class carries a particular choice of right adjoint,
-- (which is only unique up to isomorphism),
-- not merely the existence of such, and
-- so definitional properties of instances may be important.
/-- An object `X` is (right) closed if `(X âŠ— -)` is a left adjoint. -/
class RightClosed {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C] (X : C) where
  /-- a choice of a right adjoint for `tensorLeft X` -/
  rightAdj : C â¥¤ C
  /-- `tensorLeft X` is a left adjoint -/
  adj : tensorLeft X âŠ£ rightAdj

class LeftClosed {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C] (X : C) where
  /-- a choice of a right adjoint for `tensorLeft X` -/
  rightAdj : C â¥¤ C
  /-- `tensorLeft X` is a left adjoint -/
  adj : tensorRight X âŠ£ rightAdj

class Biclosed {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C] (X : C) where
  right_closed : RightClosed X
  left_closed : LeftClosed X

/-- A monoidal category `C` is (right) monoidal closed if every object is (right) closed. -/
class MonoidalRightClosed (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  right_closed (X : C) : RightClosed X := by infer_instance

class MonoidalLeftClosed (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  left_closed (X : C) : LeftClosed X := by infer_instance

class MonoidalBiclosed (C : Type u) [Category.{v} C] [MonoidalCategory.{v} C] where
  biclosed (X : C) : Biclosed X := by infer_instance

attribute [instance 100] MonoidalRightClosed.right_closed
attribute [instance 100] MonoidalLeftClosed.left_closed

variable {C : Type u} [Category.{v} C] [MonoidalCategory.{v} C]

/-- If `X` and `Y` are closed then `X âŠ— Y` is.
This isn't an instance because it's not usually how we want to construct internal homs,
we'll usually prove all objects are closed uniformly.
-/
def tensorRightClosed {X Y : C} (hX : RightClosed X) (hY : RightClosed Y) : RightClosed (X âŠ— Y) where
  rightAdj := RightClosed.rightAdj X â‹™ RightClosed.rightAdj Y
  adj := (hY.adj.comp hX.adj).ofNatIsoLeft (MonoidalCategory.tensorLeftTensor X Y).symm

def tensorLeftClosed {X Y : C} (hX : LeftClosed X) (hY : LeftClosed Y) : LeftClosed (X âŠ— Y) where
  rightAdj := LeftClosed.rightAdj Y â‹™ LeftClosed.rightAdj X
  adj := (hX.adj.comp hY.adj).ofNatIsoLeft (MonoidalCategory.tensorRightTensor X Y).symm

/-- The unit object is always closed.
This isn't an instance because most of the time we'll prove closedness for all objects at once,
rather than just for this one.
-/
def unitRightClosed : RightClosed (ğŸ™_ C) where
  rightAdj := ğŸ­ C
  adj := Adjunction.id.ofNatIsoLeft (MonoidalCategory.leftUnitorNatIso C).symm

def unitLeftClosed : LeftClosed (ğŸ™_ C) where
  rightAdj := ğŸ­ C
  adj := Adjunction.id.ofNatIsoLeft (MonoidalCategory.rightUnitorNatIso C).symm


variable (A B : C) {X X' Y Y' Z : C}


open CategoryTheory.Limits

section
variable [RightClosed A]

/-- This is the right internal hom `- [C]â† A`.
-/
def ihomR : C â¥¤ C :=
  RightClosed.rightAdj (X := A)

namespace ihomR

/-- The adjunction between `A âŠ— -` and `- [C]â† A`. -/
def adjunction : tensorLeft A âŠ£ ihomR A :=
  RightClosed.adj

instance : (tensorLeft A).IsLeftAdjoint  :=
  (ihomR.adjunction A).isLeftAdjoint

/-- The evaluation natural transformation. -/
def ev : ihomR A â‹™ tensorLeft A âŸ¶ ğŸ­ C :=
  (ihomR.adjunction A).counit

/-- The coevaluation natural transformation. -/
def coev : ğŸ­ C âŸ¶ tensorLeft A â‹™ ihomR A :=
  (ihomR.adjunction A).unit

@[simp]
theorem ihom_adjunction_counit : (ihomR.adjunction A).counit = ev A :=
  rfl

@[simp]
theorem ihom_adjunction_unit : (ihomR.adjunction A).unit = coev A :=
  rfl

@[reassoc (attr := simp)]
theorem ev_naturality {X Y : C} (f : X âŸ¶ Y) :
    A â— (ihomR A).map f â‰« (ev A).app Y = (ev A).app X â‰« f :=
  (ev A).naturality f

@[reassoc (attr := simp)]
theorem coev_naturality {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (coev A).app Y = (coev A).app X â‰« (ihomR A).map (A â— f) :=
  (coev A).naturality f

set_option quotPrecheck false in
/-- `A âŸ¶[C] B` denotes the right internal hom from `A` to `B` -/
notation B " [" C "]â† " A:10 => (@ihomR C _ _ A _).obj B

@[reassoc (attr := simp)]
theorem ev_coev : (A â— (coev A).app B) â‰« (ev A).app (A âŠ— B) = ğŸ™ (A âŠ— B) :=
  (ihomR.adjunction A).left_triangle_components _

@[reassoc (attr := simp)]
theorem coev_ev : (coev A).app (B [C]â† A) â‰« (ihomR A).map ((ev A).app B) = ğŸ™ (B [C]â† A) :=
  Adjunction.right_triangle_components (ihomR.adjunction A) _

end ihomR

instance : PreservesColimits (tensorLeft A) :=
  (ihomR.adjunction A).leftAdjoint_preservesColimits

end

section
variable [LeftClosed A]

/-- This is the left internal hom `A âŸ¶[C] -`.
-/
def ihomL : C â¥¤ C :=
  LeftClosed.rightAdj (X := A)

namespace ihomL

/-- The adjunction between `- âŠ— A` and `A â†’[C] -`. -/
def adjunction : tensorRight A âŠ£ ihomL A :=
  LeftClosed.adj

instance : (tensorRight A).IsLeftAdjoint  :=
  (ihomL.adjunction A).isLeftAdjoint

/-- The evaluation natural transformation. -/
def ev : ihomL A â‹™ tensorRight A âŸ¶ ğŸ­ C :=
  (ihomL.adjunction A).counit

/-- The coevaluation natural transformation. -/
def coev : ğŸ­ C âŸ¶ tensorRight A â‹™ ihomL A :=
  (ihomL.adjunction A).unit

@[simp]
theorem ihom_adjunction_counit : (ihomL.adjunction A).counit = ev A :=
  rfl

@[simp]
theorem ihom_adjunction_unit : (ihomL.adjunction A).unit = coev A :=
  rfl

@[reassoc (attr := simp)]
theorem ev_naturality {X Y : C} (f : X âŸ¶ Y) :
    (ihomL A).map f â–· A â‰« (ev A).app Y = (ev A).app X â‰« f :=
  (ev A).naturality f

@[reassoc (attr := simp)]
theorem coev_naturality {X Y : C} (f : X âŸ¶ Y) :
    f â‰« (coev A).app Y = (coev A).app X â‰« (ihomL A).map (f â–· A) :=
  (coev A).naturality f

set_option quotPrecheck false in
/-- `A âŸ¶[C] B` denotes the left internal hom from `A` to `B` -/
notation A " â†’[" C "] " B:10 => (@ihomL C _ _ A _).obj B

@[reassoc (attr := simp)]
theorem ev_coev : ((coev A).app B â–· A) â‰« (ev A).app (B âŠ— A) = ğŸ™ (B âŠ— A) :=
  (ihomL.adjunction A).left_triangle_components _

@[reassoc (attr := simp)]
theorem coev_ev : (coev A).app (A â†’[C] B) â‰« (ihomL A).map ((ev A).app B) = ğŸ™ (A â†’[C] B) :=
  Adjunction.right_triangle_components (ihomL.adjunction A) _

end ihomL


instance : PreservesColimits (tensorRight A) :=
  (ihomL.adjunction A).leftAdjoint_preservesColimits

end

variable {A}

-- Wrap these in a namespace so we don't clash with the core versions.
namespace MonoidalRightClosed
variable [RightClosed A]

/-- Currying in a monoidal closed category. -/
def curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ X [C]â† A) :=
  (ihomR.adjunction A).homEquiv _ _

/-- Uncurrying in a monoidal closed category. -/
def uncurry : (Y âŸ¶ X [C]â† A) â†’ (A âŠ— Y âŸ¶ X) :=
  ((ihomR.adjunction A).homEquiv _ _).symm

theorem homEquiv_apply_eq (f : A âŠ— Y âŸ¶ X) : (ihomR.adjunction A).homEquiv _ _ f = curry f :=
  rfl

theorem homEquiv_symm_apply_eq (f : Y âŸ¶ X [C]â† A) :
    ((ihomR.adjunction A).homEquiv _ _).symm f = uncurry f :=
  rfl

@[reassoc]
theorem curry_natural_left (f : X âŸ¶ X') (g : A âŠ— X' âŸ¶ Y) : curry (_ â— f â‰« g) = f â‰« curry g :=
  Adjunction.homEquiv_naturality_left _ _ _

@[reassoc]
theorem curry_natural_right (f : A âŠ— X âŸ¶ Y) (g : Y âŸ¶ Y') :
    curry (f â‰« g) = curry f â‰« (ihomR _).map g :=
  Adjunction.homEquiv_naturality_right _ _ _

@[reassoc]
theorem uncurry_natural_right (f : X âŸ¶ Y [C]â† A) (g : Y âŸ¶ Y') :
    uncurry (f â‰« (ihomR _).map g) = uncurry f â‰« g :=
  Adjunction.homEquiv_naturality_right_symm _ _ _

@[reassoc]
theorem uncurry_natural_left (f : X âŸ¶ X') (g : X' âŸ¶ Y [C]â† A) :
    uncurry (f â‰« g) = _ â— f â‰« uncurry g :=
  Adjunction.homEquiv_naturality_left_symm _ _ _

@[simp]
theorem uncurry_curry (f : A âŠ— X âŸ¶ Y) : uncurry (curry f) = f :=
  (RightClosed.adj.homEquiv _ _).left_inv f

@[simp]
theorem curry_uncurry (f : X âŸ¶ Y [C]â† A) : curry (uncurry f) = f :=
  (RightClosed.adj.homEquiv _ _).right_inv f

theorem curry_eq_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ X [C]â† A) : curry f = g â†” f = uncurry g :=
  Adjunction.homEquiv_apply_eq (ihomR.adjunction A) f g

theorem eq_curry_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ X [C]â† A) : g = curry f â†” uncurry g = f :=
  Adjunction.eq_homEquiv_apply (ihomR.adjunction A) f g

-- I don't think these two should be simp.
theorem uncurry_eq (g : Y âŸ¶ X [C]â† A) : uncurry g = (A â— g) â‰« (ihomR.ev A).app X := by
  rfl

theorem curry_eq (g : A âŠ— Y âŸ¶ X) : curry g = (ihomR.coev A).app Y â‰« (ihomR A).map g :=
  rfl

theorem curry_injective : Function.Injective (curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ X [C]â† A)) :=
  (RightClosed.adj.homEquiv _ _).injective

theorem uncurry_injective : Function.Injective (uncurry : (Y âŸ¶ X [C]â† A) â†’ (A âŠ— Y âŸ¶ X)) :=
  (RightClosed.adj.homEquiv _ _).symm.injective

variable (A X)

theorem uncurry_id_eq_ev : uncurry (ğŸ™ (X [C]â† A)) = (ihomR.ev A).app X := by
  simp [uncurry_eq]

theorem curry_id_eq_coev : curry (ğŸ™ _) = (ihomR.coev A).app X := by
  rw [curry_eq, (ihomR A).map_id (A âŠ— _)]
  apply comp_id

@[reassoc (attr := simp)]
lemma whiskerLeft_curry_ihomR_ev_app (g : A âŠ— Y âŸ¶ X) :
    A â— curry g â‰« (ihomR.ev A).app X = g := by
  simp [curry_eq]

theorem uncurry_ihomR_map (g : Y âŸ¶ Y') :
    uncurry ((ihomR A).map g) = (ihomR.ev A).app Y â‰« g := by
  apply curry_injective
  rw [curry_uncurry, curry_natural_right, â† uncurry_id_eq_ev, curry_uncurry, id_comp]

/-- The internal hom out of the unit is naturally isomorphic to the identity functor. -/
def unitNatIso [RightClosed (ğŸ™_ C)] : ğŸ­ C â‰… ihomR (ğŸ™_ C) :=
  conjugateIsoEquiv (Adjunction.id (C := C)) (ihomR.adjunction (ğŸ™_ C))
    (leftUnitorNatIso C)
section Pre

variable {A B}
variable [RightClosed B]

/-- Pre-compose an internal hom with an external hom. -/
def pre (f : B âŸ¶ A) : ihomR A âŸ¶ ihomR B :=
  conjugateEquiv (ihomR.adjunction _) (ihomR.adjunction _) ((tensoringLeft C).map f)

@[reassoc (attr := simp)]
theorem id_tensor_pre_app_comp_ev (f : B âŸ¶ A) (X : C) :
    B â— (pre f).app X â‰« (ihomR.ev B).app X = f â–· (X [C]â† A) â‰« (ihomR.ev A).app X :=
  conjugateEquiv_counit _ _ ((tensoringLeft C).map f) X

@[simp]
theorem uncurry_pre (f : B âŸ¶ A) (X : C) :
    MonoidalRightClosed.uncurry ((pre f).app X) = f â–· _ â‰« (ihomR.ev A).app X := by
  simp [uncurry_eq]

@[reassoc]
lemma curry_pre_app (f : B âŸ¶ A) {X Y : C} (g : A âŠ— Y âŸ¶ X) :
    curry g â‰« (pre f).app X = curry (f â–· _ â‰« g) := uncurry_injective (by
  rw [uncurry_curry, uncurry_eq, MonoidalCategory.whiskerLeft_comp, assoc,
    id_tensor_pre_app_comp_ev, whisker_exchange_assoc, whiskerLeft_curry_ihomR_ev_app])

@[reassoc (attr := simp)]
theorem coev_app_comp_pre_app (f : B âŸ¶ A) :
    (ihomR.coev A).app X â‰« (pre f).app (A âŠ— X) = (ihomR.coev B).app X â‰« (ihomR B).map (f â–· _) :=
  unit_conjugateEquiv _ _ ((tensoringLeft C).map f) X

@[reassoc]
lemma uncurry_pre_app (f : Y âŸ¶ X [C]â† A) (g : B âŸ¶ A) :
    uncurry (f â‰« (pre g).app X) = g â–· _ â‰« uncurry f :=
  curry_injective (by
    rw [curry_uncurry, â† curry_pre_app, curry_uncurry])

@[simp]
theorem pre_id (A : C) [RightClosed A] : pre (ğŸ™ A) = ğŸ™ _ := by
  rw [pre, Functor.map_id]
  apply conjugateEquiv_id

@[simp]
theorem pre_map {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} [RightClosed Aâ‚] [RightClosed Aâ‚‚] [RightClosed Aâ‚ƒ] (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    pre (f â‰« g) = pre g â‰« pre f := by
  rw [pre, pre, pre, conjugateEquiv_comp, (tensoringLeft C).map_comp]

theorem pre_comm_ihomR_map {W X Y Z : C} [RightClosed W] [RightClosed X] (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    (pre f).app Y â‰« (ihomR W).map g = (ihomR X).map g â‰« (pre f).app Z := by simp

end Pre

/-- The internal hom functor given by the monoidal closed structure. -/
@[simps]
def internalHomR [MonoidalRightClosed C] : Cáµ’áµ– â¥¤ C â¥¤ C where
  obj X := ihomR X.unop
  map f := pre f.unop

/-- The parametrized adjunction between `curriedTensor C : C â¥¤ C â¥¤ C`
and `internalHomR : Cáµ’áµ– â¥¤ C â¥¤ C` -/
@[simps!]
def internalHomRAdjunctionâ‚‚ [MonoidalRightClosed C] :
    curriedTensor C âŠ£â‚‚ internalHomR where
  adj _ := ihomR.adjunction _

section OfEquiv

variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] [MonoidalCategory.{vâ‚‚} D]

variable (F : C â¥¤ D) {G : D â¥¤ C} (adj : F âŠ£ G)
  [F.Monoidal] [F.IsEquivalence] [MonoidalRightClosed D]

/-- Transport the property of being monoidal closed across a monoidal equivalence of categories -/
noncomputable def ofEquiv : MonoidalRightClosed C where
  right_closed X :=
    { rightAdj := F â‹™ ihomR (F.obj X) â‹™ G
      adj := (adj.comp ((ihomR.adjunction (F.obj X)).comp
          adj.toEquivalence.symm.toAdjunction)).ofNatIsoLeft
            (Iso.compInverseIso (H := adj.toEquivalence) (Functor.Monoidal.commTensorLeft F X)) }

/-- Suppose we have a monoidal equivalence `F : C â‰Œ D`, with `D` monoidal closed. We can pull the
monoidal closed instance back along the equivalence. For `X, Y, Z : C`, this lemma describes the
resulting currying map `Hom(X âŠ— Y, Z) â†’ Hom(Y, (X âŸ¶[C] Z))`. (`X âŸ¶[C] Z` is defined to be
`Fâ»Â¹(F(X) âŸ¶[D] F(Z))`, so currying in `C` is given by essentially conjugating currying in
`D` by `F.`) -/
theorem ofEquiv_curry_def {X Y Z : C} (f : X âŠ— Y âŸ¶ Z) :
    letI := ofEquiv F adj
    MonoidalRightClosed.curry f =
      adj.homEquiv Y ((ihomR (F.obj X)).obj (F.obj Z))
        (MonoidalRightClosed.curry (adj.toEquivalence.symm.toAdjunction.homEquiv (F.obj X âŠ— F.obj Y) Z
        ((Iso.compInverseIso (H := adj.toEquivalence)
          (Functor.Monoidal.commTensorLeft F X)).hom.app Y â‰« f))) := by
  -- This whole proof used to be `rfl` before https://github.com/leanprover-community/mathlib4/pull/16317.
  change ((adj.comp ((ihomR.adjunction (F.obj X)).comp
      adj.toEquivalence.symm.toAdjunction)).ofNatIsoLeft _).homEquiv _ _ _ = _
  dsimp only [Adjunction.ofNatIsoLeft]
  rw [Adjunction.mkOfHomEquiv_homEquiv]
  dsimp
  rw [Adjunction.comp_homEquiv, Adjunction.comp_homEquiv]
  rfl

/-- Suppose we have a monoidal equivalence `F : C â‰Œ D`, with `D` monoidal closed. We can pull the
monoidal closed instance back along the equivalence. For `X, Y, Z : C`, this lemma describes the
resulting uncurrying map `Hom(Y, (X âŸ¶[C] Z)) â†’ Hom(X âŠ— Y âŸ¶ Z)`. (`X âŸ¶[C] Z` is
defined to be `Fâ»Â¹(F(X) âŸ¶[D] F(Z))`, so uncurrying in `C` is given by essentially conjugating
uncurrying in `D` by `F.`) -/
theorem ofEquiv_uncurry_def {X Y Z : C} :
    letI := ofEquiv F adj
    âˆ€ (f : Y âŸ¶ (ihomR X).obj Z), MonoidalRightClosed.uncurry f =
      ((Iso.compInverseIso (H := adj.toEquivalence)
          (Functor.Monoidal.commTensorLeft F X)).inv.app Y) â‰«
            (adj.toEquivalence.symm.toAdjunction.homEquiv _ _).symm
              (MonoidalRightClosed.uncurry ((adj.homEquiv _ _).symm f)) := by
  intro f
  -- This whole proof used to be `rfl` before https://github.com/leanprover-community/mathlib4/pull/16317.
  change (((adj.comp ((ihomR.adjunction (F.obj X)).comp
      adj.toEquivalence.symm.toAdjunction)).ofNatIsoLeft _).homEquiv _ _).symm _ = _
  dsimp only [Adjunction.ofNatIsoLeft]
  rw [Adjunction.mkOfHomEquiv_homEquiv]
  dsimp
  rw [Adjunction.comp_homEquiv, Adjunction.comp_homEquiv]
  rfl

end OfEquiv

end MonoidalRightClosed

namespace MonoidalLeftClosed
variable [LeftClosed A]

/-- Currying in a monoidal closed category. -/
def curry : (Y âŠ— A âŸ¶ X) â†’ (Y âŸ¶ A â†’[C] X) :=
  (ihomL.adjunction A).homEquiv _ _

/-- Uncurrying in a monoidal closed category. -/
def uncurry : (Y âŸ¶ A â†’[C] X) â†’ (Y âŠ— A âŸ¶ X) :=
  ((ihomL.adjunction A).homEquiv _ _).symm

theorem homEquiv_apply_eq (f : Y âŠ— A âŸ¶ X) : (ihomL.adjunction A).homEquiv _ _ f = curry f :=
  rfl

theorem homEquiv_symm_apply_eq (f : Y âŸ¶ A â†’[C] X) :
    ((ihomL.adjunction A).homEquiv _ _).symm f = uncurry f :=
  rfl

@[reassoc]
theorem curry_natural_left (f : X âŸ¶ X') (g : X' âŠ— A âŸ¶ Y) : curry (f â–· _ â‰« g) = f â‰« curry g :=
  Adjunction.homEquiv_naturality_left _ _ _

@[reassoc]
theorem curry_natural_right (f : X âŠ— A âŸ¶ Y) (g : Y âŸ¶ Y') :
    curry (f â‰« g) = curry f â‰« (ihomL _).map g :=
  Adjunction.homEquiv_naturality_right _ _ _

@[reassoc]
theorem uncurry_natural_right (f : X âŸ¶ A â†’[C] Y) (g : Y âŸ¶ Y') :
    uncurry (f â‰« (ihomL _).map g) = uncurry f â‰« g :=
  Adjunction.homEquiv_naturality_right_symm _ _ _

@[reassoc]
theorem uncurry_natural_left (f : X âŸ¶ X') (g : X' âŸ¶ A â†’[C] Y) :
    uncurry (f â‰« g) = f â–· _ â‰« uncurry g :=
  Adjunction.homEquiv_naturality_left_symm _ _ _

@[simp]
theorem uncurry_curry (f : X âŠ— A âŸ¶ Y) : uncurry (curry f) = f :=
  (LeftClosed.adj.homEquiv _ _).left_inv f

@[simp]
theorem curry_uncurry (f : X âŸ¶ A â†’[C] Y) : curry (uncurry f) = f :=
  (LeftClosed.adj.homEquiv _ _).right_inv f

theorem curry_eq_iff (f : Y âŠ— A âŸ¶ X) (g : Y âŸ¶ A â†’[C] X) : curry f = g â†” f = uncurry g :=
  Adjunction.homEquiv_apply_eq (ihomL.adjunction A) f g

theorem eq_curry_iff (f : Y âŠ— A âŸ¶ X) (g : Y âŸ¶ A â†’[C] X) : g = curry f â†” uncurry g = f :=
  Adjunction.eq_homEquiv_apply (ihomL.adjunction A) f g

-- I don't think these two should be simp.
theorem uncurry_eq (g : Y âŸ¶ A â†’[C] X) : uncurry g = (g â–· A) â‰« (ihomL.ev A).app X := by
  rfl

theorem curry_eq (g : Y âŠ— A âŸ¶ X) : curry g = (ihomL.coev A).app Y â‰« (ihomL A).map g :=
  rfl

theorem curry_injective : Function.Injective (curry : (Y âŠ— A âŸ¶ X) â†’ (Y âŸ¶ A â†’[C] X)) :=
  (LeftClosed.adj.homEquiv _ _).injective

theorem uncurry_injective : Function.Injective (uncurry : (Y âŸ¶ A â†’[C] X) â†’ (Y âŠ— A âŸ¶ X)) :=
  (LeftClosed.adj.homEquiv _ _).symm.injective

variable (A X)

theorem uncurry_id_eq_ev : uncurry (ğŸ™ (A â†’[C] X)) = (ihomL.ev A).app X := by
  simp [uncurry_eq]

theorem curry_id_eq_coev : curry (ğŸ™ _) = (ihomL.coev A).app X := by
  rw [curry_eq, (ihomL A).map_id (_ âŠ— A)]
  apply comp_id

@[reassoc (attr := simp)]
lemma whiskerRight_curry_ihomL_ev_app (g : Y âŠ— A âŸ¶ X) :
    curry g â–· A â‰« (ihomL.ev A).app X = g := by
  simp [curry_eq]

theorem uncurry_ihomL_map (g : Y âŸ¶ Y') :
    uncurry ((ihomL A).map g) = (ihomL.ev A).app Y â‰« g := by
  apply curry_injective
  rw [curry_uncurry, curry_natural_right, â† uncurry_id_eq_ev, curry_uncurry, id_comp]

/-- The internal hom out of the unit is naturally isomorphic to the identity functor. -/
def unitNatIso [LeftClosed (ğŸ™_ C)] : ğŸ­ C â‰… ihomL (ğŸ™_ C) :=
  conjugateIsoEquiv (Adjunction.id (C := C)) (ihomL.adjunction (ğŸ™_ C))
    (rightUnitorNatIso C)
section Pre

variable {A B}
variable [LeftClosed B]

/-- Pre-compose an internal hom with an external hom. -/
def pre (f : B âŸ¶ A) : ihomL A âŸ¶ ihomL B :=
  conjugateEquiv (ihomL.adjunction _) (ihomL.adjunction _) ((tensoringRight C).map f)

@[reassoc (attr := simp)]
theorem id_tensor_pre_app_comp_ev (f : B âŸ¶ A) (X : C) :
    (pre f).app X â–· B â‰« (ihomL.ev B).app X = ((A â†’[C] X) â— f) â‰« (ihomL.ev A).app X :=
  conjugateEquiv_counit _ _ ((tensoringRight C).map f) X

@[simp]
theorem uncurry_pre (f : B âŸ¶ A) (X : C) :
    MonoidalLeftClosed.uncurry ((pre f).app X) = _ â— f â‰« (ihomL.ev A).app X := by
  simp [uncurry_eq]

@[reassoc]
lemma curry_pre_app (f : B âŸ¶ A) {X Y : C} (g : Y âŠ— A âŸ¶ X) :
    curry g â‰« (pre f).app X = curry (_ â— f â‰« g) := uncurry_injective (by
  rw [uncurry_curry, uncurry_eq, MonoidalCategory.comp_whiskerRight, assoc,
    id_tensor_pre_app_comp_ev, â† whisker_exchange_assoc, whiskerRight_curry_ihomL_ev_app])

@[reassoc (attr := simp)]
theorem coev_app_comp_pre_app (f : B âŸ¶ A) :
    (ihomL.coev A).app X â‰« (pre f).app (X âŠ— A) = (ihomL.coev B).app X â‰« (ihomL B).map (_ â— f) :=
  unit_conjugateEquiv _ _ ((tensoringRight C).map f) X

@[reassoc]
lemma uncurry_pre_app (f : Y âŸ¶ A â†’[C] X) (g : B âŸ¶ A) :
    uncurry (f â‰« (pre g).app X) = _ â— g â‰« uncurry f :=
  curry_injective (by
    rw [curry_uncurry, â† curry_pre_app, curry_uncurry])

@[simp]
theorem pre_id (A : C) [LeftClosed A] : pre (ğŸ™ A) = ğŸ™ _ := by
  rw [pre, Functor.map_id]
  apply conjugateEquiv_id

@[simp]
theorem pre_map {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} [LeftClosed Aâ‚] [LeftClosed Aâ‚‚] [LeftClosed Aâ‚ƒ] (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    pre (f â‰« g) = pre g â‰« pre f := by
  rw [pre, pre, pre, conjugateEquiv_comp, (tensoringRight C).map_comp]

theorem pre_comm_ihomL_map {W X Y Z : C} [LeftClosed W] [LeftClosed X] (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    (pre f).app Y â‰« (ihomL W).map g = (ihomL X).map g â‰« (pre f).app Z := by simp

end Pre

/-- The internal hom functor given by the monoidal closed structure. -/
@[simps]
def internalHomL [MonoidalLeftClosed C] : Cáµ’áµ– â¥¤ C â¥¤ C where
  obj X := ihomL X.unop
  map f := pre f.unop

/-- The parametrized adjunction between `curriedTensor C : C â¥¤ C â¥¤ C`
and `internalHomL : Cáµ’áµ– â¥¤ C â¥¤ C` -/
@[simps!]
def internalHomLAdjunctionâ‚‚ [MonoidalLeftClosed C] :
    (curriedTensor C).flip âŠ£â‚‚ internalHomL where
  adj _ := ihomL.adjunction _

-- TODO ofEquiv for ihomL

end MonoidalLeftClosed
